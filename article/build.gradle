plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
}

bootJar {
	enabled = false
}

ext {
	set('springCloudVersion', "2023.0.0")
}

allprojects {
	group = 'dev.practice'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

subprojects {
	sourceCompatibility = '17'
	apply plugin: 'java'

	dependencies {
		compileOnly 'org.projectlombok:lombok:1.18.30'
		annotationProcessor 'org.projectlombok:lombok:1.18.30'

		testCompileOnly 'org.projectlombok:lombok:1.18.30'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(':presentation') {

	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':core')

		/**
		 * 코드상으론 의존성이 없지만(compile 단계)..
		 * presentation module 에 있는 Spring boot application 에서 runtime 에 persistence module 의 bean 을 스캔하려면.. 필요함..
		 * 따라서, runtimeOnly..
		 *
		 * gradle 로 article > other > compileJava 를 수행하고 run 하면 동작하지만..
		 * build clean 하고 컴파일하지 않고 그냥 run 을 하면 error..
		 * 그래서 그냥 implementation 으로 둔다..
		 */
		implementation project(':persistence')
		implementation project(':network')

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'io.projectreactor:reactor-test'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}

project(':persistence') {

	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':core')

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'io.projectreactor:reactor-test'
	}
}

project(':network') {

	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':core')

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'io.projectreactor:reactor-test'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}


